<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="Assistant" script:language="StarBasic" script:moduleType="normal">REM  *****  BASIC  *****
Option Explicit

Public oAssistantDlg As Object
Public aTypes(10) As Object
Public statisticMethods(5) As Object
Public IsRunning As Boolean

Const TYPE_NORMAL	  As String = &quot;可為任意文字或數值資料。&quot;
Const TYPE_BOOLEAN	  As String = &quot;&apos;true&apos;或&apos;false&apos;。&quot;
Const TYPE_IMAGE	  As String = &quot;一般常見的圖片格式轉成 Base64 格式傳送。&quot;
Const TYPE_ENUM		  As String = &quot;從 1 開始的正整數。對應列舉清單，依據數字顯示對應的項目。&quot;
Const TYPE_STATISTIC  As String = &quot;選擇目標欄位進行統計函數運算。&quot;

Type NumberFormatType
	FormatName	As String
	ImageUrl	As String
	ValueType	As String
	BaseKey		As Long
	DefaultKey	As Long
	ValueName	As String
	SpecialData As String
	Description As String
End Type

Type StatisticFormatType
	MethodName  As String
	Description As String
End Type

Type GroupArray
	GroupName as String
	Member as String
End Type

Type GroupVariable
	RealName As String
	GroupName As  String
End Type

Sub Main
	Dim oDisposeListener As Object
	Dim aType As Object
	Dim sMethod As Object
	Dim I As Long
	
	&apos; 初始化統計方法的資料, 目前最多定義 10 總方法
	For I = LBound(statisticMethods) To UBound(statisticMethods)
		statisticMethods(I) = createObject(&quot;StatisticFormatType&quot;)
		Select Case I
			Case 0
				statisticMethods(I).MethodName  = &quot;總和&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄總和&quot; 
			Case 1
				statisticMethods(I).MethodName = &quot;中位數&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄的中位數&quot;
			Case 2
				statisticMethods(I).MethodName = &quot;最大值&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄的最大值&quot;
			Case 3
				statisticMethods(I).MethodName = &quot;最小值&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄的最小值&quot;
			Case 4
				statisticMethods(I).MethodName = &quot;平均&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄的平均值&quot;
			Case 5
				statisticMethods(I).MethodName = &quot;計數&quot;
				statisticMethods(I).Description = &quot;計算指定群組欄的非空白格總數&quot;
			Case Else
				statisticMethods(I).MethodName = &quot;&quot;
				statisticMethods(I).Description = &quot;&quot;
		End Select
	Next I
	
	&apos; 初始化變數型別的資料
	For I = LBound(aTypes) To UBound(aTypes)
		aTypes(I) = createObject(&quot;NumberFormatType&quot;)
		Select Case I
			Case 0
				aTypes(I).FormatName	= &quot;一般&quot;
				aTypes(I).ValueType		= &quot;auto&quot;
				aTypes(I).BaseKey		= 0
				aTypes(I).DefaultKey	= 0
				aTypes(I).ValueName		= &quot;&quot;
				aTypes(I).Description	= &quot;可為任意文字或數值資料。&quot;
			Case 1
				aTypes(I).FormatName	= &quot;數字&quot;
				aTypes(I).ValueType		= &quot;float&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.NUMBER
				aTypes(I).DefaultKey	= 2
				aTypes(I).ValueName		= &quot;value&quot;
				aTypes(I).Description	= &quot;可帶小數的數值資料。&quot;
			Case 2
				aTypes(I).FormatName	= &quot;百分比&quot;
				aTypes(I).ValueType		= &quot;percentage&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.PERCENT
				aTypes(I).DefaultKey	= 11
				aTypes(I).ValueName		= &quot;value&quot;
				aTypes(I).Description	= &quot;可帶小數的數值資料。如 0.32 表示 32%、1.5 表示 150%...&quot;
			Case 3
				aTypes(I).FormatName	= &quot;貨幣&quot;
				aTypes(I).ValueType		= &quot;currency&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.CURRENCY
				aTypes(I).DefaultKey	= 104
				aTypes(I).ValueName		= &quot;value&quot;
				aTypes(I).Description	= &quot;可帶小數的數值資料。&quot;
			Case 4
				aTypes(I).FormatName	= &quot;日期&quot;
				aTypes(I).ValueType		= &quot;date&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.DATE
				aTypes(I).DefaultKey	= 30
				aTypes(I).ValueName		= &quot;date-value&quot;
				aTypes(I).Description	= &quot;一律用ISO8601格式表示。如(2018-8-5)。&quot;
			Case 5
				aTypes(I).FormatName	= &quot;時間&quot;
				aTypes(I).ValueType		= &quot;time&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.TIME
				aTypes(I).DefaultKey	= 41
				aTypes(I).ValueName		= &quot;time-value&quot;
				aTypes(I).Description	= &quot;一律用ISO8601格式表示。如 13:09:45 格式為&apos;PT13H09M45S&apos;。&quot;
			Case 6
				aTypes(I).FormatName	= &quot;日期/時間&quot;
				aTypes(I).ValueType		= &quot;date&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.DATETIME
				aTypes(I).DefaultKey	= 50
				aTypes(I).ValueName		= &quot;date-value&quot;
				aTypes(I).Description	= &quot;一律用ISO8601格式表示。如2018年8月5日 13:09:45格式為&apos;2018-8-5T13:09:45&apos;。&quot;
			Case 7
				aTypes(I).FormatName	= &quot;科學&quot;
				aTypes(I).ValueType		= &quot;float&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.SCIENTIFIC
				aTypes(I).DefaultKey	= 61
				aTypes(I).ValueName		= &quot;value&quot;
				aTypes(I).Description	= &quot;可帶小數的數值資料。&quot;
			Case 8
				aTypes(I).FormatName	= &quot;分數&quot;
				aTypes(I).ValueType		= &quot;float&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.FRACTION
				aTypes(I).DefaultKey	= 70
				aTypes(I).ValueName		= &quot;value&quot;
				aTypes(I).Description	= &quot;可帶小數的數值資料。&quot;
			Case 9
				aTypes(I).FormatName	= &quot;布林值&quot;
				aTypes(I).ValueType		= &quot;boolean&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.LOGICAL
				aTypes(I).DefaultKey	= 99
				aTypes(I).ValueName		= &quot;boolean-value&quot;
				aTypes(I).Description	= &quot;&apos;true&apos; 或 &apos;false&apos;&quot;
			Case 10
				aTypes(I).FormatName	= &quot;文字&quot;
				aTypes(I).ValueType		= &quot;string&quot;
				aTypes(I).BaseKey		= com.sun.star.util.NumberFormat.TEXT
				aTypes(I).DefaultKey	= 100
				aTypes(I).ValueName		= &quot;&quot;
				aTypes(I).Description	= &quot;一般文字。&quot;
		End Select
	Next I

	&apos; 確保只有一個 Dialog 被執行
	If (Not DialogIsRunning(oAssistantDlg)) Then
	
			
		IsRunning = True
		Set oDisposeListener = CreateUnoListener(_
							&quot;AssistantDialog_&quot;, &quot;com.sun.star.lang.XEventListener&quot;)
		Set oAssistantDlg =  LoadModelessDialog(LIB_NAME, &quot;AssistantDialog&quot;)
		
		&apos;透過 python 取得插件路徑
		Dim path
		Dim delIMGPath
		Dim cellSettingIMGPath
		Dim obj 
		obj = CreateUnoService(&quot;ReportAssistant.getExtensionPath&quot;)
	
		if not isNull(obj) then
			path = obj.execute(array())
		endif

		With oAssistantDlg
		
			&apos;自製圖片
			delIMGPath =  &quot;file:///&quot; + path + &quot;images/delete.png&quot;
			cellSettingIMGPath =  &quot;file:///&quot; + path + &quot;images/gear.png&quot;
			.Model.DelField.ImageURL = delIMGPath
			.Model.AdvanceSetting.ImageURL = cellSettingIMGPath
			With .Model
				.Step = 1
				.Tag = Tools.Misc.GetDocumentType(ThisComponent)
				.ReportAssistantButton.ImageURL = getIconURL(&quot;design-icon.png&quot;)
				.ParagraphStylesButton.ImageURL = getIconURL(&quot;paragraph-icon.png&quot;)
				.NumberingStylesButton.ImageURL = getIconURL(&quot;numbering-icon.png&quot;)
				&apos;.DelField.ImageURL = getIconURL(&quot;numbering-icon.png&quot;)
				.PersonalButton.ImageURL = getIconURL(&quot;personal-information-icon.png&quot;)
				.WaterMarkButton.ImageURL = getIconURL(&quot;watermark-icon.png&quot;)
				.BindingLineButton.ImageURL = getIconURL(&quot;stapler-icon.png&quot;)
				If (.Tag = &quot;scalc&quot;) Then
					
					.AdvanceSetting.EnableVisible = True
					.FormatList.EnableVisible = True
					.DelField.EnableVisible = False
					
					&apos;統計變數只有在 calc 可看見
					.GroupVariableList.EnableVisible = True
					.StatisticType.EnableVisible = True
					.StatisticList.EnableVisible = True

					.ParagraphStylesButton.Enabled = False
					.NumberingStylesButton.Enabled = False
					.WaterMarkButton.Enabled = False
					.BindingLineButton.Enabled = False
					
				Else
					
					.AdvanceSetting.EnableVisible = False
					.FormatList.EnableVisible = False
					.DelField.EnableVisible = True
					.GroupVariableList.EnableVisible = False
					.StatisticType.EnableVisible = False
					.StatisticList.EnableVisible = False

					.ParagraphStylesButton.Enabled = True
					.NumberingStylesButton.Enabled = True
					.WaterMarkButton.Enabled = True
					.BindingLineButton.Enabled = True
					
				End If
			End With	&apos; Model

			RA_ClearAssistantDialog()

			With .getControl(&quot;FormatList&quot;).Model
				I = 0
				For Each aType In aTypes
					.insertItemText(I, aType.FormatName)
					.setItemData(I, aType)
					I = I + 1
				Next aType
			End With
			
			With .getControl(&quot;StatisticList&quot;).Model
				I = 0
				For Each sMethod In statisticMethods
					If sMethod.MethodName &lt;&gt; &quot;&quot; Then
						.insertItemText(I, sMethod.MethodName)
						.setItemData(I, sMethod)
					End If
					I = I + 1
				Next sMethod	
			End With
			.getControl(&quot;StatisticList&quot;).selectItemPos(0, True)
			
			.addEventListener(oDisposeListener)
			.setVisible(True)
			RA_SEL_selectionChanged()
			&apos; 加入標記偵測
			RA_registerSelectionChenge(ThisComponent)
			With oAssistantDlg.Model
				If (.Tag = &quot;scalc&quot;) Then
					updateGroupVariableList()
				End If
			End With
			
			Do While IsRunning
				&apos; 如果文件被關掉，Dialog.Visible 會被設成 False
				IsRunning = .isVisible()
				Wait 200
			Loop
			.setVisible(False)
		End With	&apos; oAssistantDlg
		
		
		
	Else
		Msgbox(&quot;範本設計精靈執行中&quot;)
	End If

End Sub

Function AssistantDialog_disposing(oEvent As com.sun.star.lang.EventObject)
	RA_unRegisterSelectionChenge(ThisComponent)
	IsRunning = False
End Function


&apos; 清除
Function RA_ClearAssistantDialog()
	IF NOT IsRunning Then
		Exit Function
	END IF
	With oAssistantDlg.Model
		.InsertBtn.Label = &quot;插入文件(~I)&quot;
		.InsertBtn.Enabled = False
		.DelField.Enabled = False
		.Description.Text = &quot;&quot;
		With .FieldName
			.Text = &quot;&quot;
			.removeAllItems()
			.Tag = &quot;&quot;
		End With
		Type_Change()
		RA_SetDetailArea(False)
	End With
End Function

Function RA_SetDetailArea(bSW As Boolean)
	If isNull(oAssistantDlg) Then
		Exit Function
	End if
	With oAssistantDlg
		With .getControl(&quot;DetailFrame&quot;)
			.setEnable(bSW)
		End With
		With .getControl(&quot;DetailText&quot;)
			.setEnable(bSW)
			.setText(&quot;&quot;)
			.setVisible(bSW)
		End With
		With .getControl(&quot;DetailButton&quot;)
			.setEnable(False)
			.setVisible(bSW)
		End With
	End With

End Function

&apos;
Function FieldName_OnChange(oEvent as Object)

	With oAssistantDlg.Model
		RA_Button_OnOff(.FieldName, .InsertBtn)
	End With

End Function

Function DetailText_OnChange(oEvent As Object)
	With oAssistantDlg.Model
		RA_Button_OnOff(.DetailText, .DetailButton)
	End With
End Function


Function Item_OnChange(oEvent As Object)
	With oAssistantDlg.Model
		RA_Button_OnOff(.Item, .AddBtn)
	End With
End Function

Function InsertBtn_Click()
	Dim sTmp As String

	sTmp = Trim(oAssistantDlg.Model.FieldName.Text)
	If (Len(sTmp) = 0 ) Then
		MsgBox &quot;欄位名稱必須輸入&quot;
		Exit Function
	End If
	
	RA_InsertField()	&apos; 插入或更新欄位
	RA_ClearAssistantDialog()

End Function

Public Function AdvanceSetting_Click(oEvent As Object)

	Select Case oAssistantDlg.Model.Tag
		Case &quot;swriter&quot;
		Case &quot;scalc&quot;
			Dim document   as object
			Dim dispatcher as Object
			document   = ThisComponent.CurrentController.Frame
			dispatcher = createUnoService(&quot;com.sun.star.frame.DispatchHelper&quot;)
			dispatcher.executeDispatch(document, &quot;.uno:FormatCellDialog&quot;, &quot;&quot;, 0, Array())
			SyncFormatList()
	End Select
End Function


&apos; 插入或更新欄位
Function RA_InsertField()
	Dim oResource As Object
	Dim sTmp$
	Dim sFieldText$, sHint$
	Dim sType$, sFormat$, sSize$, sItems$, sDescription$, sApiHelp$
	Dim sMethod$, sColumn$, sGroupName as String
	Dim nPos As Long
	Dim oField As Object
	Dim oCell As Object

	With oAssistantDlg

		If (.Model.Tag = &quot;scalc&quot;) Then
			&apos; 自己抓目前選擇的 Cell
			Set oCell = ThisComponent.currentSelection
			&apos; 單一 Cell 才行
			If (Not oCell.supportsService(&quot;com.sun.star.sheet.SheetCell&quot;)) Then
				Msgbox(&quot;欄位只能插入單一儲存格中......&quot;)
				Exit Function
			End If
		End If

		Set oResource = New ResourceClass
		
		sFieldText = Trim(.getControl(&quot;FieldName&quot;).getText())
		oResource.setPrimaryKey(sFieldText)

		sType = &quot;&quot;
		sFormat = &quot;&quot;
		sSize = &quot;&quot;
		sItems = &quot;&quot;
		sDescription = &quot;&quot;
		sMethod = &quot;&quot;
		sApiHelp = &quot;&quot;
		sColumn = &quot;&quot;
		sGroupName = &quot;&quot;

		If (.getControl(&quot;StringType&quot;).State) Then
			If (.Model.Tag = &quot;scalc&quot;) Then
				sType = getCellType(oCell, sFormat, sApiHelp)
			Else
				sType = &quot;String&quot;
				sApiHelp = TYPE_NORMAL
			End If
		ElseIf (.getControl(&quot;BooleanType&quot;).State) Then
			sType = &quot;Boolean&quot;
			sItems = Trim(.getControl(&quot;BooleanTrueItem&quot;).getText()) &amp; &quot;,&quot; &amp; _
					Trim(.getControl(&quot;BooleanFalseItem&quot;).getText())
			sApiHelp = TYPE_BOOLEAN
		ElseIf (.getControl(&quot;ImageType&quot;).State) Then
			sType = &quot;Image&quot;
			sSize = .getControl(&quot;WidthFld&quot;).getText() &amp; &quot;x&quot; &amp; _
					.getControl(&quot;HeightFld&quot;).getText()
			sApiHelp = TYPE_IMAGE
		ElseIf (.getControl(&quot;EnumType&quot;).State) Then
			sType = &quot;Enum&quot;
			sItems = Join(.getControl(&quot;ItemList&quot;).model.StringItemList, &quot;,&quot;)
			sApiHelp = TYPE_ENUM
		ElseIf (.getControl(&quot;StatisticType&quot;).State) Then
			If ( NOT ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;)) Then
				Msgbox(&quot;統計變數功能只支援試算表!&quot;)
				Exit Function
			End If
			sType = &quot;Statistic&quot;
			&apos; 把指定的變數名稱塞入 sItem
			Dim currentPos as Long 
			if .getControl(&quot;GroupVariableList&quot;).Model.ItemCount = 0 Then
				Msgbox(&quot;沒有選擇群組變數&quot;)
				Exit Function
			end if
			currentPos = .getControl(&quot;GroupVariableList&quot;).getSelectedItemPos()
			sItems = .getControl(&quot;GroupVariableList&quot;).Model.getItemData(currentPos).RealName
			sMethod = .getControl(&quot;StatisticList&quot;).getSelectedItem()
			
			sColumn = findCellsByVariableName(sItems)
			sGroupName  = findGroupNameByVariableName(sItems)
			&apos;If (sColumn = &quot;&quot; or sGroupName = &quot;&quot;) Then
			&apos;	Msgbox(&quot;查無此變數，請確認一次!&quot;)
			&apos;	Exit Function
			&apos;End If 
			sApiHelp = TYPE_STATISTIC
		Else
			MsgBox &quot;欄位型態未指定！&quot;
			Exit Function
		EndIf

		oResource.setValue(&quot;Type&quot;, sType)
		If (sFormat &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;Format&quot;, sFormat)
		End If
		If (sItems &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;Items&quot;, sItems)
		End If
		If (sSize &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;Size&quot;, sSize)
		End If
		&apos;Below is only for statistic 
		If (sMethod &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;Method&quot;, sMethod)
		End If
		If (sColumn &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;Column&quot;, sColumn)
		End If
		If (sGroupName &lt;&gt; &quot;&quot;) Then
			oResource.setValue(&quot;GroupName&quot;, sGroupName)
		End If
		oResource.setValue(&quot;Description&quot;, Trim(.getControl(&quot;Description&quot;).getText())
		oResource.setValue(&quot;ApiHelp&quot;, sApiHelp)

		&apos; 組成Type
		sHint = Resource2TypeSetting(oResource)

		With .Model.FieldName
			&apos; 新增
			If (.ItemCount = 0 OR .Tag = &quot;&quot;) Then
				RA_MakeField(ThisComponent, sFieldText, sHint)
			Else &apos; 更新
				nPos = Val(.Tag)
				Set oField = .getItemData(nPos)
				Select Case RA_GetFieldType(oField)
					Case &quot;JumpEdit&quot;
						oField.PlaceHolder = sFieldText
						oField.Hint = sHint
					Case &quot;URL&quot;
						oField.Representation = sFieldText
						oField.TargetFrame = sHint
				Case Else
					MsgBox &quot;未知欄位 → &quot; &amp; RA_GetFieldType(oField)
					Exit Function
				End Select
			End If
		End With
	End With &apos; oAssistantDlg

End Function


&apos; 透過使用者指定的變數名稱進行全域(A1:AMJ1048576)查找
Function findCellsByVariableName(target) as String
	IF oAssistantDlg.Model.Tag = &quot;swriter&quot; Then
		findCellsByVariableName = &quot;&quot;
		Exit Function
	END IF
	Dim oStatis as Object
	Dim sMethod as String
	oStatis = oAssistantDlg.getControl(&quot;StatisticList&quot;)
	sMethod = oStatis.getSelectedItem()
	Dim searchDis
	Dim oSheet
	Dim oRange, oRanges
	Dim nonEmpty as String
	oSheet = ThisComponent.Sheets(0)
	oRange = oSheet.getCellRangeByName(&quot;A1:AMJ1048576&quot;)
	
	&apos; 這是　Calc 內建的 Query Method，目的在於找出儲存格內部值為 String 的 Cell
	oRanges = oRange.queryContentCells(com.sun.star.sheet.CellFlags.STRING)
    
    
    &apos; 把找到的 Cell 進行過濾
    Dim oAddrs, oAddr
    Dim i As Long
    Dim nRow As Long
    Dim nCol As long
    Dim s As String
    Dim oCell
    oAddrs() = oRanges.getRangeAddresses()
  	For i = 0 To UBound(oAddrs())
 
	    REM Get a specific address range
	    oAddr = oAddrs(i)
	    For nRow = oAddr.StartRow To oAddr.EndRow
			For nCol = oAddr.StartColumn To oAddr.EndColumn
				oCell = oRange.Spreadsheet.getCellByPosition(nCol, nRow)
				If (target = oCell.getString()) Then
					If (ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;)) Then
						&apos; 檢查是不是變數(HyperLink)
						If (checkCalcCellLink(oCell)) Then
							&apos; 檢查同一列有沒有群組變數(詳細資料)加上去的註解
							If (checkGroup(oCell) &lt;&gt; &quot;&quot;) Then
								findCellsByVariableName = oCell.AbsoluteName
								Exit Function
							End If
						End If
					End If
				End IF
			Next
	    Next
	Next
	
    findCellsByVariableName = &quot;&quot;

End Function

&apos; 透過使用者指定的變數名稱進行全域(A1:AMJ1048576)查找
Function findGroupNameByVariableName(target) as String
	IF not ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;) Then
		findGroupNameByVariableName = &quot;&quot;
		Exit Function
	END IF
	Dim oStatis as Object
	Dim sMethod as String
	oStatis = oAssistantDlg.getControl(&quot;StatisticList&quot;)
	sMethod = oStatis.getSelectedItem()
	Dim searchDis
	Dim oSheet
	Dim oRange, oRanges
	Dim nonEmpty as String
	oSheet = ThisComponent.Sheets(0)
	oRange = oSheet.getCellRangeByName(&quot;A1:AMJ1048576&quot;)
	
	&apos; 這是　Calc 內建的 Query Method，目的在於找出儲存格內部值為 String 的 Cell
	oRanges = oRange.queryContentCells(com.sun.star.sheet.CellFlags.STRING)
    
    
    &apos; 把找到的 Cell 進行過濾
    Dim oAddrs, oAddr
    Dim i As Long
    Dim nRow As Long
    Dim nCol As long
    Dim s As String
    Dim oCell
    Dim groupName as String
    oAddrs() = oRanges.getRangeAddresses()
  	For i = 0 To UBound(oAddrs())
 
	    REM Get a specific address range
	    oAddr = oAddrs(i)
	    For nRow = oAddr.StartRow To oAddr.EndRow
			For nCol = oAddr.StartColumn To oAddr.EndColumn
				oCell = oRange.Spreadsheet.getCellByPosition(nCol, nRow)
				If (target = oCell.getString()) Then
					If (ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;)) Then
						&apos; 檢查是不是變數(HyperLink)
						If (checkCalcCellLink(oCell)) Then
							&apos; 檢查同一列有沒有群組變數(詳細資料)加上去的註解
							groupName = checkGroup(oCell)
							If (groupName &lt;&gt; &quot;&quot;) Then
								findGroupNameByVariableName = groupName 
								Exit Function
							End If
						End If
					End If
				End IF
			Next
	    Next
	Next
	
    findGroupNameByVariableName = &quot;&quot;

End Function

&apos;確認是不是群組變數, 如果是群組就回傳群組名稱
Function checkGroup(aCell) as String
	Dim result As Object
	Dim annotations As Object
	Dim annotationIndex As Long
	Dim anAnnotation As Object
	Dim isFound As Boolean
	
	checkGroup = &quot;&quot;
	annotations = aCell.spreadSheet.annotations
	While ((annotationIndex &lt; annotations.count) and (not isFound))
		anAnnotation = annotations.getByIndex(annotationIndex)
		isFound = ((anAnnotation.position.row = aCell.cellAddress.row))
		If (isFound) Then
			&apos;annotation access video 	https://www.youtube.com/watch?v=ZpBAsu5MCzU
			checkGroup = anAnnotation.String
		End If
		annotationIndex = annotationIndex + 1
	Wend
End Function

&apos;確認群組變數名稱是否有重複
Function checkGroupNameDuplicated(newName) as Boolean
	Dim annotations As Object
	Dim annotationIndex As Long
	Dim anAnnotation As Object
	Dim oSelection as Object
	Dim oSheet as Object
	Set oSelection = ThisComponent.CurrentSelection
	Set oSheet = oSelection.Spreadsheet
	checkGroupNameDuplicated = False
	annotations = oSheet.annotations
	While (annotationIndex &lt; annotations.count)
		anAnnotation = annotations.getByIndex(annotationIndex)
		If (anAnnotation.String = newName) Then
			checkGroupNameDuplicated = True
		End If
		annotationIndex = annotationIndex + 1
	Wend
End Function

&apos; 確認找到的元素是不是 Hyper Link (因為我們就是插入 Hyper Link)

Function CheckCalcCellLink(oCell) as Boolean
	Dim oText, oParEnum, oParElement
	Dim oEnum, oElement 
	oParEnum = oCell.getText().createEnumeration()
	checkCalcCellLink = False
	Do While oParEnum.hasMoreElements ()
		oParElement = oParEnum.nextElement()
		oEnum = oParElement.createEnumeration()
		Do While oEnum.hasMoreElements ()
			oElement = oEnum.nextElement()
			If oElement.TextPortionType = &quot;TextField&quot; Then
				If oElement.TextField.supportsService(&quot;com.sun.star.text.TextField.URL&quot;) Then
					&apos;STRING  Representation = 
					&apos;STRING  TargetFrame = 
					&apos;STRING  URL = 
					CheckCalcCellLink = True
				End If
			End If
		Loop
	Loop
End Function


&apos; 內部測試用的巨集
Function TestTest() as String
	Msgbox(&quot;Hello&quot;)
	Dim oSelection as Object
	Dim textField as Object
	Set oSelection = ThisComponent.getCurrentSelection()
	textField = oSelection.getTextFields()
	Msgbox(oSelection.getString())
	Msgbox(RA_GetFieldType(textField.getByIndex(0)))
	Dim oField as Object
	oField = textField.getByIndex(0)
	Dim sFieldName$, sHint$
	sFieldName = oField.Representation
	sHint = oField.TargetFrame
	Msgbox(sHint)
	Dim oResource as Object
	Set oResource = TypeSetting2Resource(sFieldName, sHint)

	With oResource
		Msgbox(.getValue(&quot;Type&quot;))
	end with
End Function

&apos;Return Resource of the Calc variable
Function getCalcVariableResource(oCell) as Object
	Dim textField as Object
	textField = oCell.getTextFields()

	Dim oField as Object
	oField = textField.getByIndex(0)
	Dim sFieldName$, sHint$
	sFieldName = oField.Representation
	sHint = oField.TargetFrame

	Dim oResource as Object
	Set oResource = TypeSetting2Resource(sFieldName, sHint)
	getCalcVariableResource = oResource
End function

Function isStatisticVariable(oCell) as Boolean
	Dim oResource as Object
	oResource = getCalcVariableResource(oCell)
	isStatisticVariable = False
	with oResource
		if .getValue(&quot;Type&quot;) = &quot;Statistic&quot; Then
			isStatisticVariable = True
			Exit Function
		end if
	end with
End Function

Function updateGroupVariableList()
	IF not ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;) Then
		findGroupNameByVariableName = &quot;&quot;
		Exit Function
	END IF
	Dim I
	Dim allGV() as Object
	allGV = findAllGroupVariable()
	if Ubound(allGV) = 1 Then
		if allGv(0).RealName=&quot;&quot; Then
			Exit Function
		end if
	end if 
	With oAssistantDlg
		With .getControl(&quot;GroupVariableList&quot;)
			if .Model.ItemCount &lt;&gt; 0 Then
				.Model.removeAllItems()
			End if
			Dim gpName as String
			gpName = &quot;&quot;
			Dim pos as Long
			pos = -1
			Dim itemIndex as Long
			For I = 0 to Ubound(allGV)-1
				pos = -1
				For itemIndex = 0 to .Model.ItemCount-1
					Dim tmpData as Object
					if .Model.ItemCount = 0 Then 
						Exit For
					end if 
					tmpData = .Model.getItemData(itemIndex)
					if tmpData.GroupName = allGV(I).GroupName THen
						pos = itemIndex
						
						Exit for
					end if  
				Next
				
				if pos = -1 then
					pos = .Model.ItemCount 
					.Model.insertItemText(pos, allGV(I).GroupName + &quot; : &quot; + allGV(I).RealName)
					.Model.setItemData(pos, allGV(I))
				else
					Dim space as String
					space = &quot;&quot;
					Dim sp as Long
					For sp = 0 to Len(allGV(I).GroupName)
						space = space + &quot; &quot;
					Next
					.Model.insertItemText(pos+1, space + allGV(I).RealName)
					.Model.setItemData(pos+1, allGV(I))
				End if
			Next
		End With
		.getControl(&quot;GroupVariableList&quot;).selectItemPos(0, True)
	end with
End Function

&apos; 找到全部的群組變數
Function findAllGroupVariable() 
	IF not ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;) Then
		findGroupNameByVariableName = &quot;&quot;
		Exit Function
	END IF
	Dim oSheet
	Dim oRange, oRanges
	Dim nonEmpty as String
	oSheet = ThisComponent.Sheets(0)
	oRange = oSheet.getCellRangeByName(&quot;A1:AMJ1048576&quot;)
	
	&apos; 這是　Calc 內建的 Query Method，目的在於找出儲存格內部值為 String 的 Cell
	oRanges = oRange.queryContentCells(com.sun.star.sheet.CellFlags.STRING)
    
    
    &apos; 把找到的 Cell 進行過濾
    Dim oAddrs, oAddr
    Dim i As Long
    Dim nRow As Long
    Dim nCol As long
    Dim s As String
    Dim oCell
    oAddrs() = oRanges.getRangeAddresses()
    Dim result(1) as Object
    result(0) = createObject(&quot;GroupVariable&quot;)
	result(0).RealName = &quot;&quot;
	result(0).GroupName = &quot;&quot;
    Dim index As Long
    index = 0
  	For i = 0 To UBound(oAddrs())
 
	    REM Get a specific address range
	    oAddr = oAddrs(i)
	    For nRow = oAddr.StartRow To oAddr.EndRow
			For nCol = oAddr.StartColumn To oAddr.EndColumn
				oCell = oRange.Spreadsheet.getCellByPosition(nCol, nRow)
				If (True) Then
					If (ThisComponent.supportsService(&quot;com.sun.star.sheet.SpreadsheetDocument&quot;)) Then
						&apos; 檢查是不是變數(HyperLink)
						
						If (checkCalcCellLink(oCell)) Then
							&apos; 檢查同一列有沒有群組變數(詳細資料)加上去的註解
							
							Dim gpName as String
							gpName = checkGroup(oCell)
							If (gpName &lt;&gt; &quot;&quot; and isStatisticVariable(oCell) &lt;&gt; True )Then
								result(index) = createObject(&quot;GroupVariable&quot;)
								result(index).RealName = oCell.getString()
								result(index).GroupName = gpName
								index = index + 1
								if index &gt; Ubound(result)-1 Then
									ReDim Preserve result(0 to index) as Object
								END if
							End If
						End If
					End If
				End IF
			Next
	    Next
	Next		
	findAllGroupVariable = result()
End Function

&apos; 刪除欄位
Function RA_DelField()
	Dim nPos As Long
	Dim oField As Object

	With oAssistantDlg.Model.FieldName
		nPos = Val(.Tag)
		Set oField = .getItemData(nPos)
		oField.dispose()
		
	End With
	RA_ClearAssistantDialog()
	oAssistantDlg.getControl(&quot;DelField&quot;).setEnable(False)
End Function

Function getCellType(oCell As Object, ByRef sFormat, ByRef sApiHelp) As String
	Dim oFormats As Object
	Dim oKey As Object
	Dim nNumberFormat As Long
	Dim nUserDefined As Long
	Dim oType As Object

	sFormat = &quot;&quot;
	sApiHelp = aTypes(0).Description
	getCellType = &quot;auto&quot;

	nNumberFormat = oCell.NumberFormat
	If (nNumberFormat = 0) Then
		Exit Function
	End If

	Set oFormats = ThisComponent.getNumberFormats()
	Set oKey = oFormats.getByKey(nNumberFormat)
	nUserDefined = IIF(oKey.UserDefined, com.sun.star.util.NumberFormat.DEFINED, 0)

	For Each oType In aTypes
		If (oType.BaseKey = oKey.Type - nUserDefined) Then
			getCellType = oType.ValueType
			sApiHelp = oType.Description
			If (oType.ValueName &lt;&gt; &quot;&quot;) Then
				sFormat = oType.ValueName
			End If
			&apos; 貨幣
			&apos;If (oKey.CurrencyAbbreviation &lt;&gt; &quot;&quot;) Then
			&apos;	getCellType = getCellType &amp; &quot;&quot;
			&apos;End If
			Exit Function
		End If
	Next oType
End Function

Function SyncFormatList(Optional oCell As Object)
	If isNull(oAssistantDlg) Then
		Exit Function
	End if
	Dim oSelection As Object
	Dim I As Long
	Dim oFormats As Object
	Dim oKey As Object
	Dim oFList As Object
	Dim nNumberFormat As Long
	Dim nUserDefined As Long

	&apos; 如果沒有傳 Cell 物件
	If (isMissing(oCell)) Then
		&apos; 自己抓目前選擇的 Cell
		Set oSelection = ThisComponent.currentSelection
		&apos; 單一 Cell 才行
		If (Not oSelection.supportsService(&quot;com.sun.star.sheet.SheetCell&quot;)) Then
			Exit Function
		End If
		Set oCell = oSelection
	End If
	nNumberFormat = oCell.NumberFormat
	oFList = oAssistantDlg.getControl(&quot;FormatList&quot;)
	If (nNumberFormat = 0) Then
		oFList.selectItemPos(0, True)
		Exit Function
	End If

	Set oFormats = ThisComponent.getNumberFormats()
	Set oKey = oFormats.getByKey(oCell.NumberFormat)

	nUserDefined = IIF(oKey.UserDefined, com.sun.star.util.NumberFormat.DEFINED, 0)

	With oAssistantDlg
		Set oFList = .getControl(&quot;FormatList&quot;)
		With oFList.Model
			For I = 0 To .ItemCount - 1
				If (.getItemData(I).BaseKey = oKey.Type - nUserDefined) Then
					oFList.selectItemPos(I, True)
					Exit For
				End If
			Next I
			If (I &gt;= .ItemCount) Then
				.getItemData(9).BaseKey
				Debug oKey
			End If
		End With

	End With

End Function

Function ShowApiHelp(sString As String)
	oAssistantDlg.getControl(&quot;API_Help&quot;).Text = sString
End Function

Function FormatList_change(oEvent As com.sun.star.awt.ItemEvent)
	Dim oSelection As Object
	Dim oFList As Object
	Dim oType As Object

	Set oSelection = ThisComponent.currentSelection
	If (Not oSelection.supportsService(&quot;com.sun.star.sheet.SheetCell&quot;)) Then
		Exit Function
	End If

	Set oFList = oEvent.Source
	With oFList.Model
		Set oType = .getItemData(oEvent.Selected)
		oSelection.NumberFormat = oType.DefaultKey
		ShowApiHelp(oType.Description)
	End With
	
End Function


&apos; 統計資料列表選項變更的 callback 
Function StatisticList_onChange(oEvent As com.sun.star.awt.ItemEvent)
	Dim oSelection As Object
	Dim oFList As Object
	Dim oType As Object

	Set oSelection = ThisComponent.currentSelection
	If (Not oSelection.supportsService(&quot;com.sun.star.sheet.SheetCell&quot;)) Then
		Exit Function
	End If

	Set oFList = oEvent.Source
	With oFList.Model
		Set oType = .getItemData(oEvent.Selected)
		&apos;oSelection.NumberFormat = oType.DefaultKey
		ShowApiHelp(oType.Description)
	End With
	
End Function

Private Function m_ShowFormatListApiHelp()
	Dim oType As Object

	With oAssistantDlg.Model.FormatList
		If (UBound(.SelectedItems) &gt;= 0) Then
			Set oType = .getItemData(.SelectedItems(0))
			ShowApiHelp(oType.Description)
		End If
	End With
End Function

Function DetailButton_OnClick(oEvent As Object)

	Dim oDoc As Object
	Dim sDocType As String

	Set oDoc = ThisComponent
	sDocType = Tools.Misc.getDocumentType(oDoc)

	
	Select Case oAssistantDlg.Model.Tag
		Case &quot;swriter&quot;
			RA_MakeAnnotationField(ThisComponent, _
							oAssistantDlg.getControl(&quot;DetailText&quot;).getText())
		Case &quot;scalc&quot;
			Dim oSheet As Object
			Dim oSelection As Object
			Dim oRangeAddr As Object
			Dim oCellAddress As New com.sun.star.table.CellAddress
			Dim oAnnotations As Object
			Dim I As Long
			Dim oAnno As Object
			Dim bFound As boolean

			Set oSelection = oDoc.CurrentSelection
			Set oSheet = oSelection.Spreadsheet
			Set oRangeAddr = oSelection.RangeAddress
			Set oAnnotations = oSheet.Annotations
			&apos;Make sure that the group variable name is unique
			Dim newName 
			newName = oAssistantDlg.getControl(&quot;DetailText&quot;).getText()
			If checkGroupNameDuplicated(newName) Then
				Msgbox(&quot;群組變數名稱已存在&quot;)	
				Exit Function
			End If
			
			&apos; 設定群組
			oSheet.group(oRangeAddr, com.sun.star.table.TableOrientation.ROWS)

			&apos; 插入註解當作名稱(在群組第一列第一格)
			oCellAddress.Sheet = oRangeAddr.Sheet
			oCellAddress.Column = 0
			oCellAddress.Row = oRangeAddr.StartRow
			oAnnotations.insertNew(oCellAddress, _
							oAssistantDlg.getControl(&quot;DetailText&quot;).getText())
			bFound = False
			For I = 0 To oAnnotations.getCount() - 1
				Set oAnno =  oAnnotations.getByIndex(I)
				If (oAnno.Position.Row = oCellAddress.Row And _
					oAnno.Position.Column = oCellAddress.Column) Then
					bFound = True
					oAnno.setIsVisible(True)
					Exit For
				End If
			Next I

	End Select
	RA_ClearAssistantDialog()
End Function


Function AddBtn_Click(oEvent As Object)
	Dim oTextbox As Object

	With oAssistantDlg
		Set oTextbox = .getControl(&quot;Item&quot;)
		Tools.Listbox.AddSingleItemToListbox(.getControl(&quot;ItemList&quot;).model, oTextbox.getText())
		oTextbox.setText(&quot;&quot;)
		oTextbox.setFocus()
	End With
	
End Function

Function Type_Change(Optional oEvent As Object)

	m_Change_FormatControls()
	m_Change_BooleanControls()
	m_Change_ImageControls()
	m_Change_EnumControls()
	m_Change_StatisticControls()
End Function

Private Function m_Change_FormatControls()
	Dim bSW As Boolean

	With oAssistantDlg.Model
		bSW = .StringType.State
		.FormatList.Enabled = bSW
		.StringType.Label = &quot;一~般&quot;
		If (.Tag &lt;&gt; &quot;scalc&quot;) Then
			If (bSW) Then
				ShowApiHelp(TYPE_NORMAL)
			End If
		Else
			.StringType.Label = &quot;格~式&quot;
			If (bSW) Then
				m_ShowFormatListApiHelp()
			End If
		End If
	End With

End Function


Private Function m_Change_StatisticControls()
	Dim bSW As Boolean

	With oAssistantDlg.Model
		If (.Tag = &quot;scalc&quot;) Then
			bSW = .StatisticType.State
			.StatisticList.Enabled = bSW
			.GroupVariableList.Enabled = bSW
		
			If (bSW) Then
				ShowApiHelp(TYPE_STATISTIC)
			End If
		End If
	End With

End Function

Private Function m_Change_BooleanControls()
	Dim bSW As Boolean

	With oAssistantDlg.Model
		bSW = .BooleanType.State
		.BooleanTrueLbl.Enabled = bSW
		.BooleanFalseLbl.Enabled = bSW
		.BooleanTrueItem.Enabled = bSW
		.BooleanFalseItem.Enabled = bSW
		If (bSW) Then
			ShowApiHelp(TYPE_BOOLEAN)
		End If
	End With
End Function

Private Function m_Change_ImageControls()
	Dim bSW As Boolean

	With oAssistantDlg.Model
		bSW = .ImageType.State
		.WidthLbl.Enabled = bSW
		.WidthFld.Enabled = bSW
		.HeightLbl.Enabled = bSW
		.HeightFld.Enabled = bSW
		If (Not bSW) Then
			.WidthFld.Value = 0
			.HeightFld.Value = 0
		Else
			ShowApiHelp(TYPE_IMAGE)
		End If
	End With

End Function

Private Function m_Change_EnumControls()
	Dim bSW as Boolean

	With oAssistantDlg.Model
		bSW = .EnumType.State
		.ItemListLbl.Enabled = bSW
		.Item.Enabled = bSW
		.ItemList.Enabled = bSW
		.AddBtn.Enabled = False
		.UpBtn.Enabled = False
		.DownBtn.Enabled = False
		.DelBtn.Enabled = False
		If ( Not bSW) Then
			.Item.Text = &quot;&quot;
			Tools.Listbox.EmptyListbox(.ItemList)
		Else
			ShowApiHelp(TYPE_ENUM)
		End If
	End With

End Function

Function DelBtn_Click(oEvent As Object)
	Dim nPos%
	Dim oListControl As Object

	With oAssistantDlg
		oListControl = .getControl(&quot;ItemList&quot;)
		nPos = oListControl.getSelectedItemPos()
		If (nPos &gt;= 0) Then
			oListControl.removeItems(nPos, 1)
			&apos; 刪除光光
			If (oListControl.getItemCount() &lt; 1) Then
				.getControl(&quot;UpBtn&quot;).setEnable(False)
				.getControl(&quot;DownBtn&quot;).setEnable(False)
				.getControl(&quot;DelBtn&quot;).setEnable(False)
				.getControl(&quot;Item&quot;).setFocus()
			End If
		End If
	End With
End Function

Function UpBtn_Click(oEvent As Object)
	RA_ItemMoveUp(oAssistantDlg.getControl(&quot;ItemList&quot;))
End Function

Function DownBtn_Click(oEvent As Object)
	RA_ItemMoveDown(oAssistantDlg.getControl(&quot;ItemList&quot;))
End Function

Function ItemList_onChange(oEvent As Object)
	With oAssistantDlg
		.getControl(&quot;UpBtn&quot;).setEnable(True)
		.getControl(&quot;DownBtn&quot;).setEnable(True)
		.getControl(&quot;DelBtn&quot;).setEnable(True)
	End With
End Function

Function RA_FieldSelect(Optional oField As Object)
	If isNull(oAssistantDlg) Then
		Exit Function
	End if
	Dim sName As String, sHint As String
	Dim nItemPos As Long
	Dim nLegalFields As Long
	Dim I As Long
	Dim oFld As Object

	&apos; 沒有傳參數，就清除所有欄位
	If (IsMissing(oField)) Then
		RA_ClearAssistantDialog()
		Exit Function
	End If

	With oAssistantDlg.Model.FieldName
		.removeAllItems()
		&apos; 傳來佔位項欄位
		Select Case RA_GetFieldType(oField)
			Case &quot;JumpEdit&quot;
				sName = oField.PlaceHolder
				sHint = oField.Hint
				nItemPos = .ItemCount
				.insertItemText(nItemPos, sName)
				.setItemData(nItemPos, oField)
				.Tag = nItemPos
				RA_ParserField(oField)
			&apos;傳來儲存格欄位集合
			Case &quot;TextFields&quot;
				nLegalFields = 0
				For I = 1 To oField.Count
					oFld = oField.getByIndex(I - 1)
					If (Ra_GetFieldType(oFld) = &quot;URL&quot;) Then
						sName = oFld.Representation
						sHint = oFld.TargetFrame
						If (Left(sHint,5) = &quot;Type:&quot;) Then
							nItemPos = .ItemCount
							.insertItemText(nItemPos, sName)
							.setItemData(nItemPos, oFld)
							nLegalFields = nLegalFields + 1
						End If
					End If
				Next I

				Select Case nLegalFields
					Case 0
						RA_ClearAssistantDialog()
						Exit Function
					Case 1
						.Tag = 0
						RA_ParserField(.getItemData(0))
					Case Else
						.Tag = &quot;&quot;
						.Text = &quot;&quot;
						Change_FormatControls(False)
						Change_ImageControls(False)
						Change_EnumControls(False)
				End Select
		End Select
		oAssistantDlg.getControl(&quot;InsertBtn&quot;).setLabel(&quot;更新(~C)&quot;)
		oAssistantDlg.getControl(&quot;DelField&quot;).setEnable(True)
	End With

End Function

&apos;還原選中的欄位數值到 dialog
Function RA_ParserField(oField As Object)
	Dim oResource As Object
	Dim aItems() As String
	Dim aSize() As String
	Dim sFieldName$, sHint$

	Select Case RA_GetFieldType(oField)
		Case &quot;JumpEdit&quot;	&apos;佔位項(預留位置、萬用欄位)
			sFieldName = oField.PlaceHolder
			sHint = oField.Hint
		Case &quot;URL&quot;
			sFieldName = oField.Representation
			sHint = oField.TargetFrame
		Case Else
			Exit Function
	End Select

	Set oResource = TypeSetting2Resource(sFieldName, sHint)

	oAssistantDlg.getControl(&quot;FieldName&quot;).setText(sFieldName)

	With oResource
		Select Case .getValue(&quot;Type&quot;)
			Case &quot;Boolean&quot;
				oAssistantDlg.getControl(&quot;BooleanType&quot;).setState(True)
			Case &quot;Image&quot;
				oAssistantDlg.getControl(&quot;ImageType&quot;).setState(True)
			Case &quot;Enum&quot;
				oAssistantDlg.getControl(&quot;EnumType&quot;).setState(True)
			Case &quot;Statistic&quot;
				oAssistantDlg.getControl(&quot;StatisticType&quot;).setState(True)
			Case Else
				oAssistantDlg.getControl(&quot;StringType&quot;).setState(True)
		End Select
		Type_Change()	&apos; 變更欄位型態

		If (.hasByName(&quot;Size&quot;)) Then
			aSize = Split(.getValue(&quot;Size&quot;), &quot;x&quot;)
			If (UBound(aSize) = 1) Then
				oAssistantDlg.getControl(&quot;WidthFld&quot;).setText(aSize(0))
				oAssistantDlg.getControl(&quot;HeightFld&quot;).setText(aSize(1))
			End If
		End If

		If (.hasByName(&quot;Items&quot;)) Then
			aItems() = Split(.getValue(&quot;Items&quot;), &quot;,&quot;)
			If (oAssistantDlg.getControl(&quot;BooleanType&quot;).getState()) Then
				oAssistantDlg.getControl(&quot;BooleanTrueItem&quot;).setText(aItems(0))
				oAssistantDlg.getControl(&quot;BooleanFalseItem&quot;).setText(aItems(1))
			ElseIF oAssistantDlg.getControl(&quot;StatisticType&quot;).getState() Then
				Dim itemIndex as Long
				itemIndex = 0
				Dim gpName as String
				gpName = .getValue(&quot;GroupName&quot;)
				with oAssistantDlg.getControl(&quot;GroupVariableList&quot;)
					for itemIndex=0 to .Model.ItemCount-1
						Dim itemData as Object
						itemData = .Model.getItemData(itemIndex)
						if itemData.RealName = aItems(0) and itemData.GroupName = gpName Then
							.selectItemPos(itemIndex, True)
							Exit for
						ENd if 
					Next itemIndex
				end with
			Else
				oAssistantDlg.getControl(&quot;ItemList&quot;).model.StringItemList() = aItems()
			End If
		End If
		
		If (.hasByName(&quot;Method&quot;)) Then
			Dim methodName as String
			methodName = .getValue(&quot;Method&quot;)
			If (oAssistantDlg.getControl(&quot;StatisticType&quot;).getState()) Then
				oAssistantDlg.getControl(&quot;StatisticList&quot;).selectItem(methodName, true)
			End If
		End If

		oAssistantDlg.getControl(&quot;Description&quot;).setText(.getValue(&quot;Description&quot;))
		ShowApiHelp(.getValue(&quot;ApiHelp&quot;))
	End With
End Function

Function Item_SelectChange(e As Object)
	Dim sName As String
	Dim I As Long

	With oAssistantDlg
		With .Model.FieldName
			.Tag = &quot;&quot;
			&apos; 找找看是哪一個 ItemPos(Listbox 有 getSelectItemPos函數， Combobox 沒有 XD)
			For I = LBound(.StringItemList) To UBound(.StringItemList)
				If (.StringItemList(I) = .Text) Then
					.Tag = I
					RA_ParserField(.getItemData(I))
					Exit For
				End If
			Next I
		End With
	End With
End Function
</script:module>